/**
 * Copyright 2019 Adobe. All rights reserved.
 * This file is licensed to you under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License. You may obtain a copy
 * of the License at http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under
 * the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR REPRESENTATIONS
 * OF ANY KIND, either express or implied. See the License for the specific language
 * governing permissions and limitations under the License.
 */
plugins {
    id 'com.android.application'
    id 'com.diffplug.spotless'
}

android {
    namespace 'com.adobe.marketing.tester'

    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.adobe.marketing.edgetestapp"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName project.property('moduleVersion') as String ?: "1.0.0 (Default)"
        archivesBaseName = "edge-test-app-$versionName"

        //This says that my test app only wants to use the Phone variant
        missingDimensionStrategy 'target', 'phone'

        testInstrumentationRunner rootProject.ext.testInstrumentationRunner

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

spotless {
    java {
        toggleOffOn("format:off", "format:on")
        target "src/*/java/**/*.java"
        removeUnusedImports()
        prettier(['prettier': '2.7.1', 'prettier-plugin-java': '1.6.2'])
                .config(['parser': 'java', 'tabWidth': 4, 'useTabs': true, 'printWidth': 120])
        endWithNewline()
        licenseHeaderFile "../../config/formatter/adobe.header.txt"
    }
}

dependencies {
    // Add dependencies on AEP Mobile extensions
    implementation project(':edge')
    implementation project(':app-util-xdm')

    implementation 'com.adobe.marketing.mobile:core:2.+'
    implementation 'com.adobe.marketing.mobile:edgeidentity:2.+'
    implementation('com.adobe.marketing.mobile:edgeconsent:2.+') {
        exclude group: 'com.adobe.marketing.mobile', module: 'edge'
    }
    implementation 'com.adobe.marketing.mobile:assurance:2.+'

    // project dependencies
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.code.gson:gson:2.8.9'
}

// Uncomment to disable Gradle dependency caching and get the latest version of dependencies
//configurations.all {
//    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}
