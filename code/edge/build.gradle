/**
 * Copyright 2019 Adobe. All rights reserved.
 * This file is licensed to you under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License. You may obtain a copy
 * of the License at http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under
 * the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR REPRESENTATIONS
 * OF ANY KIND, either express or implied. See the License for the specific language
 * governing permissions and limitations under the License.
 */
plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'com.diffplug.spotless'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
}

jacoco {
    toolVersion = rootProject.ext.jacocoVersion
}

android {
    namespace 'com.adobe.marketing.mobile.edge'

    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion

        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
    }

    flavorDimensions = ["target"]
    productFlavors {
        phone {
            dimension "target"
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.targetCompatibility
    }

    kotlinOptions {
        jvmTarget = rootProject.ext.kotlinJvmTarget
        languageVersion = rootProject.ext.kotlinLanguageVersion
        apiVersion = rootProject.ext.kotlinApiVersion
    }
}

preBuild.dependsOn spotlessApply

spotless {
    java {
        toggleOffOn("format:off", "format:on")
        target "src/*/java/**/*.java"
        removeUnusedImports()
        prettier(['prettier': '2.7.1', 'prettier-plugin-java': '1.6.2'])
                .config(['parser': 'java', 'tabWidth': 4, 'useTabs': true, 'printWidth': 120])
        endWithNewline()
        licenseHeaderFile "../../config/formatter/adobe.header.txt"
    }
    kotlin {
        target "src/*/java/**/*.kt"
        ktlint('0.47.0')
        licenseHeaderFile "../../config/formatter/adobe.header.txt"
    }
}

tasks.register('javadocJar', Jar) {
    dependsOn dokkaJavadoc
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}


dokkaJavadoc.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
            perPackageOption {
                matchingRegex.set(".*\\.internal.*") // proper setting
                suppress.set(true)
            }
        }

        if (!disableVariants) {
            named("phone") {
                noAndroidSdkLink.set(false)
                perPackageOption {
                    matchingRegex.set(".*\\.internal.*") // proper setting
                    suppress.set(true)
                }
            }
        }
    }
}

def isReleaseBuild() {
    return hasProperty('release')
}

version = isReleaseBuild() ? rootProject.moduleVersion : rootProject.moduleVersion+"-SNAPSHOT"

publish.dependsOn('assemblePhone')

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.adobe.marketing.mobile'
            artifactId = rootProject.moduleName
            version = version
            artifact("$buildDir/outputs/aar/${moduleAARName}")
            artifact javadocJar

            pom {
                name = mavenRepoName
                description = mavenRepoDescription
                url = 'https://developer.adobe.com/client-sdks'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'adobe'
                        name = 'adobe'
                        email = 'adobe-mobile-testing@adobe.com'
                    }
                }
                scm {
                    connection = 'scm:git:github.com//adobe/aepsdk-edge-android.git'
                    developerConnection = 'scm:git:ssh://github.com//adobe/aepsdk-edge-android.git'
                    url = 'https://github.com/adobe/aepsdk-edge-android'
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    def coreDependencyNode = dependenciesNode.appendNode('dependency')
                    coreDependencyNode.appendNode('groupId', 'com.adobe.marketing.mobile')
                    coreDependencyNode.appendNode('artifactId', 'core')
                    coreDependencyNode.appendNode('version', mavenCoreVersion)

                    def identityDependencyNode = dependenciesNode.appendNode('dependency')
                    identityDependencyNode.appendNode('groupId', 'com.adobe.marketing.mobile')
                    identityDependencyNode.appendNode('artifactId', 'edgeidentity')
                    identityDependencyNode.appendNode('version', mavenIdentityVersion)
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = version.endsWith('SNAPSHOT') ? "https://oss.sonatype.org/content/repositories/snapshots/" : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("SONATYPE_USERNAME")
                password = System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}

ext."signing.gnupg.executable"="gpg"
ext."signing.gnupg.keyName"=System.getenv("GPG_KEY_ID")
ext."signing.gnupg.passphrase"=System.getenv("GPG_PASSPHRASE")

signing {
    useGpgCmd()
    required {
        tasks.withType(PublishToMavenRepository).find {
            gradle.taskGraph.hasTask it
        }
    }
    sign publishing.publications
}

tasks.withType(Test).configureEach {
    testLogging {
        showStandardStreams = true
    }
}

tasks.register('platformUnitTestJacocoReport', JacocoReport) {
    dependsOn "testPhoneDebugUnitTest"
    def excludeRegex = ['**/BuildConfig.class']
    def javaClasses = fileTree(dir: "${project.buildDir}/intermediates/javac/phoneDebug/classes/com/adobe/marketing/mobile", excludes: excludeRegex)
    def kotlinClasses = fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/phoneDebug/classes/com/adobe/marketing/mobile", excludes: excludeRegex)

    classDirectories.setFrom(files([javaClasses, kotlinClasses]))

    additionalSourceDirs.setFrom files(android.sourceSets.main.java.sourceFiles)
    sourceDirectories.setFrom files(android.sourceSets.phone.java.sourceFiles)
    executionData fileTree(dir: "$buildDir", includes: [
            "outputs/unit_test_code_coverage/phoneDebugUnitTest/testPhoneDebugUnitTest.exec"
    ])

    // Report dir: build/reports/jacoco/platformUnitTestJacocoReport/
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
}

tasks.register('platformFunctionalTestJacocoReport', JacocoReport) {
    dependsOn "createPhoneDebugCoverageReport"
    def excludeRegex = ['**/BuildConfig.class']
    def javaClasses = fileTree(dir: "${project.buildDir}/intermediates/javac/phoneDebug/classes/com/adobe/marketing/mobile", excludes: excludeRegex)
    def kotlinClasses = fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/phoneDebug/classes/com/adobe/marketing/mobile", excludes: excludeRegex)

    classDirectories.setFrom(files([javaClasses, kotlinClasses]))
    additionalSourceDirs.setFrom files(android.sourceSets.main.java.sourceFiles)
    sourceDirectories.setFrom files(android.sourceSets.phone.java.sourceFiles)
    executionData fileTree(dir: "$buildDir", includes: [
            "outputs/code_coverage/phoneDebugAndroidTest/connected/*coverage.ec"
    ])

    // Report dir: build/reports/coverage/androidTest/phone/debug/connected/
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
}

// Uncomment to disable Gradle dependency caching and get the latest version of dependencies
//configurations.all {
//    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}

dependencies {
    implementation "com.adobe.marketing.mobile:core:${rootProject.mavenCoreVersion}"
    implementation "com.adobe.marketing.mobile:edgeidentity:${rootProject.mavenIdentityVersion}"
    implementation "androidx.annotation:annotation:${rootProject.androidxAnnotationVersion}"

    testImplementation "androidx.test.ext:junit:${rootProject.ext.junitVersion}"
    testImplementation "org.mockito:mockito-core:${rootProject.ext.mockitoVersion}"
    testImplementation "org.mockito:mockito-inline:${rootProject.ext.mockitoVersion}"
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.12.7'
    testImplementation 'org.json:json:20180813'

    androidTestImplementation('com.adobe.marketing.mobile:edgeconsent:2.+') {
        exclude group: 'com.adobe.marketing.mobile', module: 'edge'
    }

    androidTestImplementation "androidx.test.ext:junit:${rootProject.ext.junitVersion}"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation project(":test-utils")
}
